plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.xphr.reporting.ms'
version = '0.0.1-SNAPSHOT'
description = 'assessment-reporting-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    mapstructVersion = "1.6.3"
    lombokVersion = "1.18.38"
    lombokMapstructBindingVersion = "0.2.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'

    // JSP dependencies
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
    implementation 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api'
    implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl'

    // Mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}", "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    implementation("org.apache.commons:commons-lang3:3.20.0")

    implementation 'org.flywaydb:flyway-database-postgresql'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Docker Compose tasks
tasks.register('dockerComposeUp', Exec) {
    group = 'docker'
    description = 'Start PostgreSQL container using docker-compose'
    commandLine 'docker', 'compose', 'up', '-d'
    doFirst {
        println 'Starting PostgreSQL container...'
    }
    doLast {
        println 'PostgreSQL container started successfully'
    }
}

tasks.register('dockerComposeDown', Exec) {
    group = 'docker'
    description = 'Stop and remove PostgreSQL container'
    commandLine 'docker', 'compose', 'down'
    doFirst {
        println 'Stopping PostgreSQL container...'
    }
}

tasks.register('dockerComposeStatus', Exec) {
    group = 'docker'
    description = 'Show status of docker-compose services'
    commandLine 'docker', 'compose', 'ps'
}

tasks.withType(JavaCompile) {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}

// Optional: Start docker-compose before running the application
tasks.named('bootRun') {
    dependsOn 'dockerComposeUp'
}
